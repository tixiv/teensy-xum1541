; Copyright (C) 1994-2004 Joe Forster/STA <sta(at)c64(dot)org>
; All rights reserved.
;
; This file is part of OpenCBM
;
; Redistribution and use in source and binary forms, with or without
; modification, are permitted provided that the following conditions are met:
;
;     * Redistributions of source code must retain the above copyright
;       notice, this list of conditions and the following disclaimer.
;     * Redistributions in binary form must reproduce the above copyright
;       notice, this list of conditions and the following disclaimer in
;       the documentation and/or other materials provided with the
;       distribution.
;     * Neither the name of the OpenCBM team nor the names of its
;       contributors may be used to endorse or promote products derived
;       from this software without specific prior written permission.
;
; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
; IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
; TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
; PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER
; OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
; EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
; PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
; PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
; LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
; NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
; SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;

; 1541 Turbo write

.include        "address_layout.a65"

	*=$0500
	
	tr = $0a
	se = tr+1

	buf = $f9
	drv = $7f
	
	dbufptr    = $31
	n_sectors  = $43
	retry_mode = $6a
	bump_cnt   = $8d
	retry_flag = $90

	do_write   = $0400

	get_ts     = TRANSFER_CODE_START + $00
	get_block  = TRANSFER_CODE_START + $06
	send_byte  = TRANSFER_CODE_START + $09
	init       = TRANSFER_CODE_START + $0f

	jmp main

	jsr init
	ldy #$64
i0	lda $f574,y
	sta do_write-1,y
	dey
	bne i0
	ldy #$36
i1	lda $d5f8,y
	sta do_retry,y
	dey
	bpl i1
	lda #$60
	sta do_write+$64
	sta do_retry+$37
	ldx drv
	lda $feca,x	; led
	sta $026d	; mask
	ldy #$01
	sty $1c,x	; flag
	iny		; buffer ($0500)
	sty buf		; number

start	lda #$00
	sta retry_flag
	sei
	jsr get_ts
	stx tr
	sty se
	cli
	lda #$02
	sta bump_cnt
exec	lda tr
	beq done
	ldx buf
	lda #$e0	; execute buffer
	jsr $d57d	; set job parameters
wait	lda $00,x	; wait until
	bmi wait	; job has finished
check	beq exec
	cmp #$08
	beq nobump
	sta $90
	jsr $d6a6
	bcc check
	bit retry_mode
	bvs noht
	jsr do_retry
	bcc check
noht	bit retry_mode	; bump head?
	bmi nobump	; no -> skip
	dec bump_cnt
	beq nobump
	lda #$c0	; bump it!
	jsr $d57d
	jsr $d599
	bne exec
nobump	sei
	jsr send_byte
	cli
	jmp start
done	sta $1800	; A == 0
	jmp $c194

main	lda tr		; current track
	cmp $fed7	; > max. nr of tracks?
	bcc legal
	lda $1c00	; yes, set
	and #$9f	; bitrate
	sta $1c00
	lda #$11	; nr of sectors (17)
	sta n_sectors	; for tracks > 35
legal	lda #$03	; buffer address
	sta dbufptr	; (hi)
	ldy retry_flag
	bne isretry
	jsr get_block
isretry	lda $026d	; flash
	eor $1c00	; led
	sta $1c00
	jsr do_write
	lda #$00
	sta retry_flag
	jsr send_byte
	lda #$02
	sta bump_cnt
	jsr get_ts
	cpx tr		; same track?
	stx tr		; store track
	sty se		; store sector
	beq main	; yes, same track
	lda #$00	; no error
	jmp $f969	; terminate job

do_retry = *
	; rom code will be copied here and patched
	; $37 bytes length

.if (do_retry + $37) >= TRANSFER_CODE_START
.error "write code runs into transfer code"
.endif
